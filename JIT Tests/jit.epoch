//
// JIT.EPOCH
//
// Just in time compilation test for Epoch
//


timeGetTime : -> integer ms = 0 [external("WinMM.dll", "timeGetTime")]


entrypoint :
{
	integer four = 1 + 3
	assert(four == 4)

	vmbench(2, 3)
	jitbench(2, 3)
}


vmbench : integer a, integer b
{
	integer begintime = timeGetTime()
	integer result = vmmath(a, b)
	integer duration = timeGetTime() - begintime
	string durationstr = cast(string, duration)
	print("VM benchmark lasted: " ; durationstr)
	string resultstr = cast(string, result)
	print("Result: " ; resultstr)
}


jitbench : integer a, integer b
{
	integer begintime = timeGetTime()
	integer result = jitmath(a, b)
	integer duration = timeGetTime() - begintime
	string durationstr = cast(string, duration)
	print("JIT benchmark lasted: " ; durationstr)
	string resultstr = cast(string, result)
	print("Result: " ; resultstr)
}


vmmath : integer a, integer b -> integer ret = 0
{
	integer counter = 0
	integer result = 0

	while(counter < 1000000)
	{
		result = a * b
		ret = ret + result
		counter = counter + 1
	}
}


jitmath : integer a, integer b -> integer ret = 0 [native]
{
	integer counter = 0
	integer result = 0

	while(counter < 1000000)
	{
		result = a * b
		ret = ret + result
		counter = counter + 1
	}
}


