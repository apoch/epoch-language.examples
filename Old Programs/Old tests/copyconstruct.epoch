//
// COPYCONSTRUCT.EPOCH
//
// Test various forms of structure copy constructors
// Implemented as test for fixing issue #15
//


structure Test : (integer(a), integer(b), integer(c))


entrypoint : () -> ()
{
	Test(one, 1, 2, 3)
	Test(two, one)
	Test(three, functest())
	Test(four, plus(one, two))
	Test(five, one plus two)

	dump(one)
	dump(two)
	dump(three)
	dump(four)
	dump(five)
}


functest : () -> (Test(ret, 0, 0, 0))
{
	ret.a = 9
	ret.b = 8
	ret.c = 7
}

infix plus : (Test(left), Test(right)) -> (Test(ret, left.a + right.a, left.b + right.b, left.c + right.c)) { }

dump : (Test(t)) -> ()
{
	debugwritestring(cast(string, t.a) ; " " ; cast(string, t.b) ; " " ; cast(string, t.c))
}

